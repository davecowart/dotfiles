export ZSH=$HOME/.dotfiles

export PROJECTS=~/Projects

# save environment variables in ~/.localrc to keep them private
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# load all the zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)
#
# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit
zstyle ':completion:*' menu select

# change directories without cd
setopt auto_cd

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

export PATH=~/.rbenv/shims:/usr/local/bin:/usr/local/bin/git:/usr/bin:/bin:/usr/sbin:/sbin:$HOME/bin:/usr/local/sbin:~/.tmuxifier/bin:~/.dotfiles/bin

alias fuck='$(thefuck $(fc -ln -1))'
alias fix='fuck'
eval "$(thefuck --alias)"
